buildscript {
    repositories {
        //maven {
        //  url 'https://oss.sonatype.org/content/repositories/snapshots/'
        //}
        mavenCentral()
        jcenter()
    }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "rid.team.jobqueue"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        fatal 'UnusedResources'
        warning 'ResourceType' //TODO remove eventually with ButterKnife 8.0 release
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Bus Logic
    compile "com.squareup:otto:$depOttoVersion"

    // Network
    compile "com.squareup.okhttp:okhttp:$depOkHttpVersion"
    compile "com.squareup.okhttp:okhttp-urlconnection:$depOkHttpVersion"
    compile "com.squareup.retrofit:retrofit:$depRetrofitVersion"
    debugCompile "com.squareup.retrofit:retrofit-mock:$depRetrofitVersion"

    // Multi threading
    compile "com.birbit:android-priority-jobqueue:$depJobQueueVersion"

    // UI
    compile "com.android.support:design:$depAndroidSupportVersion"
    compile "com.android.support:recyclerview-v7:$depAndroidSupportVersion"
    compile "com.android.support:cardview-v7:$depAndroidSupportVersion"
    compile "com.jakewharton:butterknife:$depButterKnifeVersion"

    // AppDatabase
    compile "com.raizlabs.android:DBFlow-Core:$depDbFlowVersion"
    compile "com.raizlabs.android:DBFlow:$depDbFlowVersion"
    apt "com.raizlabs.android:DBFlow-Compiler:$depDbFlowVersion"

    // Logging
    compile "com.jakewharton.timber:timber:$depTimberVersion"

    // Inject
    apt "com.google.dagger:dagger-compiler:$depDaggerCompilerVersion"
    compile "com.google.dagger:dagger:$depDaggerCompilerVersion"
    provided 'javax.annotation:jsr250-api:1.0'
}
